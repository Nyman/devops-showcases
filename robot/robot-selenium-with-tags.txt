*** Settings ***
Library    Selenium2Library
Suite Setup    Open Browser    ${HOMEPAGE}    ${BROWSER}
Test Setup     Go To    ${HOMEPAGE}
Suite Teardown  Close All Browsers
Documentation   Demonstrate Selenium2Library by googling.

*** Variables ***
${HOMEPAGE}    http://www.google.fi
${BROWSER}     chrome

*** Test Cases ***
Google searches and verifying results
    [Tags]    templated-tests
    [Template]    Google and check results
    devops    www.eficode.fi
    deveo     deveo.com
    robot framework    robotframework.org

Google devops and find eficode
    [Tags]    devops    eficode    smoke
    Google and check results    devops    www.eficode.fi

Google deveo and find its web site
    [Tags]    deveo    eficode
    Google and check results    deveo     deveo.com

Google robot and find its web site
    [Tags]    robot
    Google and check results    robot framework    robotframework.org

*** Keywords ***
Google and check results
    [Arguments]    ${searchkey}    ${result}
    Input Text     id=gbqfq    ${searchkey}
    Click Button   id=gbqfb
    Wait Until Page Contains    ${result}
